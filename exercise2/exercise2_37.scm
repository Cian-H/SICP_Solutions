(load "lib/functools.scm")

(define (create-matrix seq stride)
  (cond ((null? seq) '())
    ((< (length seq) stride) (error "create-matrix: List length not divisible by stride"))
    (else
      (cons (take seq stride)
        (create-matrix (drop seq stride) stride)))))

(define (transpose mat)
  (accumulate-n cons '() mat))

(define (take l n)
  (if (or (null? l) (= n 0)) '() (cons (car l) (take (cdr l) (- n 1)))))

(define (drop l n)
  (if (or (null? l) (= n 0)) l (drop (cdr l) (- n 1))))

(define (pad-string str len)
  (string-append str (make-string (- len (string-length str)) #\space)))

(define (display-matrix m)
  (if (null? m)
    (display "[]")
    (let* ((str-m (map (lambda (row) (map number->string row)) m))
           (cols (transpose str-m))
           (col-widths (map (lambda (col) (apply max (map string-length col))) cols)))
      (display "[\n")
      (for-each
        (lambda (row)
          (display "  [ ")
          (let loop ((items row) (widths col-widths))
            (cond ((null? items) #t)
              (else
                (let ((s (car items))
                      (w (car widths))
                      (is-last? (null? (cdr items))))
                  (display (pad-string s w))
                  (if (not is-last?)
                    (display ", ")
                    (display " "))
                  (loop (cdr items) (cdr widths))))))
          (display "],\n"))
        str-m)

      (display "]"))))

(define (dot-product v w)
  (if (not (= (length v) (length w)))
    (error "dot-product: Vector length mismatch")
    (accumulate + 0 (map * v w))))

(define (matrix-*-vector m v)
  (map (lambda (row) (dot-product row v)) m))

(define (matrix-*-matrix m n)
  (let ((n-cols (transpose n)))
    (map (lambda (row) (matrix-*-vector n-cols row)) m)))

(define v0 (list 1 2 3 4))
(define v1 (list 5 6 7 8))
(define m0 (create-matrix (list 1 2 3 4 5 6 7 8 9 10 11 12) 4))
(define m1 (create-matrix (list 13 14 15 16 17 18 19 20 21 22 23 24) 3))

(newline)
(display "(dot-product v0 v0) => ")
(display (dot-product v0 v0))
(newline)
(display "(dot-product v0 v1) => ")
(display (dot-product v0 v1))
(newline)
(display "(dot-product v1 v0) => ")
(display (dot-product v1 v0))
(newline)
(display "(dot-product v1 v1) => ")
(display (dot-product v1 v1))
(newline)
(display "(matrix-*-vector m0 v0) => ")
(display (matrix-*-vector m0 v0))
(newline)
(display "(matrix-*-vector m0 v1) => ")
(display (matrix-*-vector m0 v1))
(newline)
(display "(matrix-*-vector (transpose m1) v0) => ")
(display (matrix-*-vector (transpose m1) v0))
(newline)
(display "(matrix-*-vector (transpose m1) v1) => ")
(display (matrix-*-vector (transpose m1) v1))
(newline)
(display "(matrix-*-matrix m0 (transpose m0)) => ")
(display-matrix (matrix-*-matrix m0 (transpose m0)))
(newline)
(display "(matrix-*-matrix m0 m1) => ")
(display-matrix (matrix-*-matrix m0 m1))
(newline)
(display "(matrix-*-matrix m1 m0) => ")
(display-matrix (matrix-*-matrix m1 m0))
(newline)
(display "(matrix-*-matrix m1 (transpose m1)) => ")
(display-matrix (matrix-*-matrix m1 (transpose m1)))
(newline)
