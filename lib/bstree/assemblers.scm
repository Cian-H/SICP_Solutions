(define (bstree-partial elts n)
  (if (= n 0)
    (cons bstree-empty elts)
    (let* ((left-size (quotient (- n 1) 2))
           (left-result (bstree-partial elts left-size))
           (left-tree (car left-result))
           (non-left-elts (cdr left-result))
           (this-entry (car non-left-elts))
           (right-size (- n (+ left-size 1)))
           (right-result (bstree-partial (cdr non-left-elts) right-size))
           (right-tree (car right-result))
           (remaining-elts (cdr right-result)))
      (cons (bstree-node this-entry left-tree right-tree) remaining-elts))))

(define (bstree-join x left right)
  (bstree-balance (bstree-node x left right)))

(define (bstree-split tree k)
  (if (bstree-empty? tree)
    (cons bstree-empty bstree-empty)
    (let ((root (bstree-root tree))
          (left (bstree-left-branch tree))
          (right (bstree-right-branch tree)))
      (cond
        ((= k root) (cons left right))
        ((< k root)
          (let* ((result (bstree-split left k))
                 (small (car result))
                 (big (cdr result)))
            (cons small (bstree-join root big right))))
        (else
          (let* ((result (bstree-split right k))
                 (small (car result))
                 (big (cdr result)))
            (cons (bstree-join root left small) big)))))))
